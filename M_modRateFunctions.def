Option Compare Database
Global lngRateID As Long
Public Function GetRateID(strGroupID As String, intPlanID As Integer, intCoverID As Integer) As Long
'------------------------------------------------
'Purpose:  Find the RateID
'------------------------------------------------
'Programmer:  Tim Cook
'Create Date: 10/26/2006
'Update Date:  10/30/2007
'Updated by:  John Criswell

On Error GoTo ErrorHandler
    
    Dim strSQL As String

    strSQL = "SELECT tblRates.RateID, tblRates.GroupID, tblRates.PlanID, " & _
                "tblRates.CoverID " & _
                "FROM tblRates " & _
                "WHERE (((tblRates.GroupID)='" & strGroupID & "') " & _
                "AND ((tblRates.PlanID)= " & intPlanID & ") " & _
                "AND ((tblRates.CoverID)= " & intCoverID & "));"
    Call LoadADORecordset(strSQL)
               
    GetRateID = g_adoRS("RateID")
    
Exit_Point:
    Exit Function
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point

End Function
Public Function FindRate(GroupID As String, CoverID As Integer, PlanID As Integer) As Double
'------------------------------------------------
'Purpose:  Find the plan rate based on plan id
'          and coverage type
'------------------------------------------------
'Programmer:  Tim Cook
'Create Date: 10/26/2006
'Update Date:  10/30/2007
'Updated by:  John Criswell

On Error GoTo ErrorHandler
    
    LoadADORecordset "uspFindRate '" & GroupID & "'," & CoverID & "," & PlanID
    
    If g_adoRS.EOF Then
        FindRate = 0
    Else
        FindRate = g_adoRS("Rate")
    End If
    g_adoRS.Close
    
Exit_Point:
    Exit Function
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point

End Function
Public Function RateUsedCheck(lngRateID As Long) As Boolean
'------------------------------------------------
'Purpose:  Find the plan rate based on plan id
'          and coverage type
'------------------------------------------------
'Programmer:  Tim Cook
'Create Date: 10/26/2006
'Update Date:
'Updated by:

On Error GoTo ErrorHandler
    
    Dim db As DAO.Database
    Dim rs As DAO.Recordset
    Dim strSQL As String
    
    Set db = CurrentDb()
    strSQL = "SELECT COUNT(*) AS Cnt from tblSubscr WHERE RateID = " & lngRateID
    Set rs = db.OpenRecordset(strSQL, dbOpenSnapshot)
    If rs.RecordCount > 0 Then
      If rs.Fields("Cnt") > 0 Then
        RateUsedCheck = True
        rs.Close
      Else
        RateUsedCheck = False
      End If
    Else
        RateUsedCheck = False
    End If
    
    Set db = Nothing
    
Exit_Point:
    Exit Function
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point
    
End Function