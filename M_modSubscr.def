Option Compare Database
Option Explicit
Public Function ChangeStatus(MyForm As Form) As Boolean

On Error GoTo PROC_ERR

    Call MyForm.cmdChangeStatus_Click
    ChangeStatus = True
    
PROC_EXIT:
  DoCmd.SetWarnings True
  DoCmd.Hourglass False
  Exit Function

PROC_ERR:
  MsgBox "Error: (" & Err.Number & ") " & Err.DESCRIPTION, vbCritical, " Function: modSubscr.ChangeSubscrStatus "
  Resume PROC_EXIT

End Function
Sub PopulateGrpSubIDs(strGroupID As String)
    'Purpose:  Adds SubIDs to tblSubscr
    '--------------------------------------------------------
    ' Parameters:  -
    ' Programmer:  John Criswell
    ' Create Date:  01/21/08
    ' Update Date:  01/21/08
    ' Updated by:  John Criswell
    ' Reason:
    '---------------------------------------------------------
    
    
    On Error GoTo ErrorHandler
    
    
    Dim db As DAO.Database
    Dim rst As DAO.Recordset
    Dim strSQL As String
    
    Set db = CurrentDb()
    
    strSQL = "SELECT tblSubscr.SubID " & _
             "FROM tblSubscr " & _
             "WHERE (((tblSubscr.SubGroupID)='" & strGroupID & "') " & _
             "AND ((tblSubscr.SubID) Is Null));"
    Set rst = db.OpenRecordset(strSQL, dbOpenDynaset, dbSeeChanges)
    If rst.RecordCount > 0 Then
        Do While Not rst.EOF
            rst.Edit
            rst.Fields("SubID") = AssignSubID()
            rst.Update
            rst.MoveNext
        Loop
    End If
    
    rst.Close
    Set db = Nothing
    
    
Exit_Point:
    Exit Sub
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point
End Sub
Function AssignSubID() As String
    ' Purpose:  Generates SubID's
    '
    '------------------------------------------------
    ' Parameters:   none
    ' Returns   :   new eight digit SubscriberID as string
    ' Programmer:   John Criswell
    ' Create Date:  11/22/07
    ' Update Date:  11/22/07
    ' Updated by:   John Criswell
    ' Update:       -----------
    ' Update Date:  10/11/2013
    ' Updated by:   John Criswell
    ' Update:       Substituted stored procedure (usp_GetSubID()
    '               for VBA code
    ' --------------------------------------------------
    
    Dim sSQL As String
    Dim sSubscriberID As String
    
    sSQL = "usp_GetSubID"
    Call GetOutPutParm(sSQL)
    If Len(g_sRtnVal) <> 8 Then
        sSubscriberID = "00000000"
    Else
        sSubscriberID = g_sRtnVal
    End If
          
    AssignSubID = sSubscriberID
    
Exit_Point:
    Exit Function
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point
End Function
Function CalcAge(DOB As Date) As Integer
    
    'Purpose:  Calculates a person's age
    '--------------------------------------------------------
    '
    ' Parameters: DOB -
    ' Returns   : Integer -
    ' Programmer:  John Criswell
    ' Create Date:  12/14/07
    ' Update Date:  12/14/07
    ' Updated by:  John Criswell
    ' Reason:
    '---------------------------------------------------------
    
    On Error GoTo ErrorHandler
    
    
    
    If Month(DOB) > Month(Date) Then
        CalcAge = DateDiff("yyyy", DOB, Date) - 1
    ElseIf Month(DOB) < Month(Date) Then
        CalcAge = DateDiff("yyyy", DOB, Date)
    ElseIf Month(DOB) = Month(Date) Then
        If Day(DOB) > Day(Date) Then
            CalcAge = DateDiff("yyyy", DOB, Date) - 1
        Else
            CalcAge = DateDiff("yyyy", DOB, Date)
        End If
    End If
    
Exit_Point:
    Exit Function
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point

End Function

Function PrtScreen_Subscr()
    ' Comments  :
    ' Parameters:  -
    ' Returns   :  -
    ' Modified  :
    '
    ' --------------------------------------------------

    On Error GoTo ErrorHandler

    Dim strDocName As String
    Dim strSubscrId As String
    Dim strWhere As String

    strDocName = "rptGrpSubscr"
    strSubscrId = Forms!frmGrpSubscr!SUBssn
    strWhere = "(([tblSubscr].[SUBssn]) = """ & strSubscrId & """)"

    DoCmd.OpenReport strDocName, acPreview, , strWhere

Exit_Point:
    Exit Function

ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point

End Function
Public Sub PrintSubscrSearchCard(strSSN As String)
    ' Comments  :  Prints a card from the menu on the subscriber search screen
    ' Parameters: strSSN -
    ' Modified  :
    '
    ' --------------------------------------------------
    
    On Error GoTo ErrorHandler
      
      Dim strSQL
    
     'print card for subscriber from frmSubscrInquire
      ExecuteSQL "DELETE FROM tmpEDI_rpt"
      strSQL = "INSERT INTO tmpEDI_Rpt ( SUBssn) " & _
                        "SELECT tblSubscr.SubSSN " & _
                        "FROM tblSubscr " & _
                        "WHERE ((tblSubscr.SubSSN)='" & strSSN & "');"
    ExecuteSQL strSQL
    
    
    DoCmd.OpenReport "rptCardMemGrp"
    
    
    'clean up
    ExecuteSQL "DELETE FROM tmpEDI_rpt"
    
           
    Exit Sub
    
Exit_Point:
    Exit Sub
    
ErrorHandler:
    MsgBox Err.DESCRIPTION & " " & Err.Number
    Resume Exit_Point
    
End Sub

Function DependentCheck(strSSN As String) As Long
    
    On Error GoTo DependentCheck_Err
    
    Dim strSQL As String
    Dim db As DAO.Database
    Dim rst As DAO.Recordset
    
    Set db = CurrentDb()
    
    strSQL = "SELECT tblDependent.DEPsubID, Count(tblDependent.ID) AS CountOfID " & _
             "FROM tblDependent GROUP BY tblDependent.DEPsubID " & _
             "HAVING (((tblDependent.DEPsubID)='" & strSSN & "'));"
    Set rst = db.OpenRecordset(strSQL, dbOpenSnapshot)
    If rst.RecordCount > 0 Then
        DependentCheck = rst.Fields("CountOfID")
        rst.Close
    Else
        DependentCheck = 0
    End If
    
On Error GoTo DependentCheck_Err
    Set db = Nothing
DependentCheck_Exit:
    Exit Function
    
DependentCheck_Err:
    MsgBox Error$
    Resume DependentCheck_Exit

End Function